plugins {
  id "scala"
  id "com.jfrog.artifactory" version "4.17.2"
  id "maven-publish"
  id 'pl.allegro.tech.build.axion-release' version '1.12.0'
}

group = "com.stackstate.sensor"
archivesBaseName = "sts-sensor-api"

scmVersion {
  tag {
    prefix = 'v'
    versionSeparator = ''
  }
}

project.version = scmVersion.version

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.scala-lang:scala-library:2.12.12'
  implementation "com.thesamet.scalapb:scalapb-runtime-grpc_2.12:0.10.8"
  implementation "com.thesamet.scalapb.zio-grpc:zio-grpc-core_2.12:0.4.0"
  implementation "io.grpc:grpc-netty:1.31.1"
}

sourceSets {
  main {
    scala {
      srcDirs = ["generated/scala"]
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

artifactoryPublish {
    dependsOn jar
}

publishing {
    publications {
      maven(MavenPublication) {
            artifactId = 'sts-sensor-api_2.12'
            artifact (sourcesJar) {
              classifier = 'sources'
            }
            from components.java
      }
    }
}

if (!project.hasProperty("artifactoryUsername")) {
  ext {
    artifactoryUsername=System.getenv("ARTIFACTORY_USERNAME")
    artifactoryPassword=System.getenv("ARTIFACTORY_PASSWORD")
  }
}

artifactory {
  publish {
    contextUrl = 'https://artifactory.stackstate.io/artifactory'
    repository {
      repoKey = 'libs'
      username = project.property("artifactoryUsername")
      password = project.property("artifactoryPassword")
    }
    defaults {
      publications ('maven')
    }
    publishBuildInfo = true
    publishArtifacts = true
    publishPom = true
  }
}
