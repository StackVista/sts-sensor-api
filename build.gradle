plugins {
  id "scala"
  id "com.jfrog.artifactory" version "4.17.2"
  id "maven-publish"
  id "com.github.prokod.gradle-crossbuild" version "0.12.0"
  id 'pl.allegro.tech.build.axion-release' version '1.12.0'
}

group = "com.stackstate.sensor"
archivesBaseName = "sts-sensor-api"

crossBuild {
  scalaVersionsCatalog = ['2.12':'2.12.12']

  builds {
    scala {
      scalaVersions = ['2.12']
    }
  }
}


scmVersion {
  tag {
    prefix = 'v'
    versionSeparator = ''
  }
}

project.version = scmVersion.version

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.scala-lang:scala-library:2.12.12'
  implementation "com.thesamet.scalapb:scalapb-runtime-grpc_?:0.10.8"
  implementation "com.thesamet.scalapb.zio-grpc:zio-grpc-core_?:0.4.0"
  implementation "io.grpc:grpc-netty:1.31.1"
}

sourceSets {
  main {
    scala {
      srcDirs = ["generated/scala"]
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

artifactoryPublish {
    dependsOn jar
}

publishing {
    publications {
        crossBuild_212(MavenPublication) {
            artifactId = 'sts-sensor-api_2.12'
            artifact crossBuildScala_212Jar
            artifact (sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}

if (!project.hasProperty("artifactoryUsername")) {
  ext {
    artifactoryUsername=System.getenv("ARTIFACTORY_USERNAME")
    artifactoryPassword=System.getenv("ARTIFACTORY_PASSWORD")
  }
}

artifactory {
  publish {
    contextUrl = 'https://artifactory.stackstate.io/artifactory'
    repository {
      repoKey = 'libs'
      username = project.property("artifactoryUsername")
      password = project.property("artifactoryPassword")
    }
    defaults {
      publications ('crossBuild_212')
    }
    publishBuildInfo = true
    publishArtifacts = true
    publishPom = true
  }
}

project.tasks.build.dependsOn('crossBuildScala_212Jar')
